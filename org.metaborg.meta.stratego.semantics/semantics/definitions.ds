module definitions

imports
  base
  shared
  stratego-signatures
  state

signature
  internal-sorts
    LetHelper

  internal-constructors
    letEval : List(Def) * Strategy -> LetHelper

  arrows
    LetHelper -leteval-> Value

rules // let - in - end

  SEnv d |- Let(sdefs, s) --> v
  where
    sdefNames(sdefs) -exid-> snames,
    SMAlloc(d, snames) -salloc-> d',
    SEnv d' |- letEval(sdefs, s) -leteval-> v
  
  VEnv e, SEnv d |- letEval([], in_s) -leteval-> v
  where
    VEnv e, SEnv d |- in_s --> v
  
  VEnv e, SEnv d |- letEval([sdef | sdefs], in_s) -leteval-> v
  where
    sdef => SDefT(sname, ss, ts, s),
    typedIds(ss) -exid-> ss',
    typedIds(ts) -exid-> ts',
    SUpdate(d, sname, Thunk(sname, ss', ts', s, e, d)) -salloc-> d',
    VEnv e, SEnv d |- letEval(sdefs, in_s) -leteval-> v
    
  // VEnv e, SEnv d |- Let([], s) --> v
  // where
  //   VEnv e, SEnv d |- s --> v
  // 
  // VEnv e, SEnv d |- Let([sdef | sdefs], in_s) --> v
  // where
  //   sdef => SDefT(sname, ss, ts, s),
  //   typedIds(ss) -exid-> ss',
  //   typedIds(ts) -exid-> ts',
  //   SAlloc(d, sname) -salloc-> d',
  //   SUpdate(d', sname, Thunk(sname, ss', ts', s, e, d')) -salloc-> d'',
  //   VEnv e, SEnv d'' |- Let(sdefs, in_s) --> v

signature
  internal-sorts
    Deffer

  internal-constructors
    topdefs : Module -> Deffer
    defs : List(Def) -> Deffer

  arrows
    Deffer -sdefs-> SEnv

rules // strategy definitions
  
  // unpack specification
  VEnv e, SEnv d |- topdefs(Specification([sigs, Strategies(ss)])) -sdefs-> d'
  where
    VEnv e, SEnv d |- defs(ss) -sdefs-> d'
  
  // bottom
  VEnv e, SEnv d |- defs([]) -sdefs-> d

  // store a definition as a thunk
  VEnv e, SEnv d |- defs([sdef | ds]) -sdefs-> d''
  where
    sdef => SDefT(sname, ss, ts, s),
    typedIds(ss) -exid-> ss',
    typedIds(ts) -exid-> ts',
    SPushBack(d, sname, Thunk(sname, ss', ts', s, e, d)) -salloc-> d',
    SEnv d' |- defs(ds) -sdefs-> d''

  // unpack annotated definition
  VEnv e, SEnv d |- defs([sdef | ds]) -sdefs-> d'
  where
    sdef => AnnoDef(annos, sdef'),
    VEnv e, SEnv d |- defs([sdef' | ds]) -sdefs-> d'

  // skip declarations of external
  VEnv e, SEnv d |- defs([sdef | ds]) -sdefs-> d'
  where
    sdef => ExtSDef(a, b, c),
    VEnv e, SEnv d |- defs(ds) -sdefs-> d'

signature
  internal-sorts
    NameExtractor

  arrows
    NameExtractor -exid-> List(String)
    
  internal-constructors
    typedIds: List(Typedid) -> NameExtractor
    sdefNames: List(Def) -> NameExtractor
    
rules // typedid name extractor

  typedIds([]) -exid-> asNILofString([])
 
  typedIds([VarDec(x, dc) | xs]) -exid-> [ x | xs' ]
  where
    typedIds(xs) -exid-> xs'

rules // SDefT name extractor

  sdefNames([]) -exid-> asNILofString([])
  
  sdefNames([SDefT(sname, ss, ts, s) | sdefs]) -exid-> [sname | snames]
  where
    sdefNames(sdefs) -exid-> snames

rules // TODO store constructor declarations




