module definitions

imports
  shared
  stratego-signatures
  state

signature
  internal-sorts
    LetHelper

  internal-constructors
    letEval : List(Def) * Strategy -> LetHelper

  arrows
    LetHelper -leteval-> Value

rules // let - in - end

  SEnv d |- Let(sdefs, s) --> v
  where
    sdefNames(sdefs) -exid-> snames,
    SParAlloc(d, snames) -salloc-> d',
    SEnv d' |- letEval(sdefs, s) -leteval-> v
  
  VEnv e, SEnv d |- letEval([], in_s) -leteval-> v
  where
    VEnv e, SEnv d |- in_s --> v
  
  VEnv e, SEnv d |- letEval([sdef | sdefs], in_s) -leteval-> v
  where
    sdef => SDefT(sname, ss, ts, s),
    typedIds(ss) -exid-> ss',
    typedIds(ts) -exid-> ts',
    SUpdate(d, sname, Thunk(sname, ss', ts', s, e, d)) -salloc-> _,
    VEnv e, SEnv d |- letEval(sdefs, in_s) -leteval-> v
    
signature
  internal-sorts
    Allocator
    AllocatorResult
    
  internal-constructors
    allocModule : Module -> Allocator
    allocDefs : List(Def) * SEnv -> Allocator
    allocdDefs : List(Def) * SEnv -> AllocatorResult 
    storeDefs : List(Def) -> Allocator

  arrows
    Allocator -allocmodule-> SEnv
    Allocator -allocsdefs-> AllocatorResult
    Allocator -storesdefs-> Bool

rules // strategy definitions
  
  SEnv d |- allocModule(Specification([_, Strategies(ss)])) -allocmodule-> d'
  where
    allocDefs(ss, d) -allocsdefs-> allocdDefs(ss', d'),
    SEnv d' |- storeDefs(ss') -storesdefs-> _
  
  allocDefs([], senv) -allocsdefs-> allocdDefs([], senv)
  
  allocDefs([AnnoDef(_, sdef') | sdefs], senv) -allocsdefs-> ad
  where
    allocDefs([sdef' | sdefs], senv) -allocsdefs-> ad
  
  allocDefs([sdef | sdefs], senv) -allocsdefs-> ad
  where
    sdef => ExtSDef(_, _, _),
    allocDefs(sdefs, senv) -allocsdefs-> ad
  
  allocDefs([sdef@SDefT(sname, _, _, _) | sdefs], senv) -allocsdefs-> allocdDefs([sdef | sdefs'], senv'')
  where
    SAlloc(senv, sname) -salloc-> senv',
    allocDefs(sdefs, senv') -allocsdefs-> allocdDefs(sdefs', senv'')
  
  storeDefs([]) -storesdefs-> true
  
  VEnv e, SEnv d |- storeDefs([SDefT(sname, ss, ts, s) | sdefs]) -storesdefs-> b
  where
    typedIds(ss) -exid-> ss',
    typedIds(ts) -exid-> ts',
    SUpdate(d, sname, Thunk(sname, ss', ts', s, e, d)) -salloc-> _,
    VEnv e, SEnv d |- storeDefs(sdefs) -storesdefs-> b
  
signature
  internal-sorts
    NameExtractor

  arrows
    NameExtractor -exid-> List(String)
    
  internal-constructors
    typedIds: List(Typedid) -> NameExtractor
    sdefNames: List(Def) -> NameExtractor
    
rules // typedid name extractor

  typedIds([]) -exid-> []
 
  typedIds([VarDec(x, _) | xs]) -exid-> [ x | xs' ]
  where
    typedIds(xs) -exid-> xs'

rules // SDefT name extractor

  sdefNames([]) -exid-> []
  
  sdefNames([SDefT(sname, _, _, _) | sdefs]) -exid-> [sname | snames]
  where
    sdefNames(sdefs) -exid-> snames

rules // TODO store constructor declarations




