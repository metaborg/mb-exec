module definitions

imports
  base
  shared
  stratego-signatures

signature
  semantic-components
    D -> Map<String, StrategyDef>

rules // let - in - end

  Let([], s) --> v
  where
    s --> v
  
  D defs |- Let([SDefT(sname, ss, ts, s) | sdefs], in-s) --> v
  where
    D { sname |--> SDefT(sname, ss, ts, s), defs } |- Let(sdefs, in-s) --> v

signature
  internal-sorts
    Deffer

  internal-constructors
    topdefs : Module -> Deffer
    defs : List(Def) -> Deffer

  arrows
    Deffer -sdefs-> D

rules // strategy definitions
  
  topdefs(Specification([sigs, Strategies(ss)])) -sdefs-> v
  where
    defs(ss) -sdefs-> v

  defs([]) -sdefs-> {}

  defs([SDefT(sname, ss, ts, s) | ds]) -sdefs-> { sname |--> SDefT(sname, ss, ts, s), defs}
  where
    defs(ds) -sdefs-> defs

rules // TODO store constructor declarations
