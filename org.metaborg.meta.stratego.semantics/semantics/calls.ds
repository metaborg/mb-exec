module calls

imports
  base
  stratego-signatures
  shared
  build
  definitions
  aterm
  
rules // calls

  D defs |- (CallT(SVar(sname), ass, ats), E env) --> (v, E env'')
  where
    defs[sname] => SDefT(sname', fss, fts, s),
    bindSVars(ass, fss) -bind-> defs',
    bl(ats) -blds-> ats', // evaluate term argument expressions
    typedIds(fts) -exid-> fts',
    E env |- binder(fts', ats') -binder-> env',
    D {defs', defs} |- (s, E env') --> (v, E env'')
  
  
  // SVar(sname) --> v
  // where
  //   CallT(SVar(sname), [], []) --> v

signature
  sorts
    NameExtractor
    MultiExtender
    
  internal-constructors
    typedIds: List(Typedid) -> NameExtractor
    binder: List(String) * List(T) -> MultiExtender
    
  arrows
    NameExtractor -exid-> List(String)
    MultiExtender -binder-> E
    
rules

  E e |- binder([], []) -binder-> e
  
  E e |- binder([k | kxs], [v | vxs]) -binder-> e'
  where
    E {k |--> v, e} |- binder(kxs, vxs) -binder-> e'

rules
  
  typedIds([]) -exid-> asNILofString([])
 
  typedIds([VarDec(x, dc) | xs]) -exid-> [ x | xs' ]
  where
    typedIds(xs) -exid-> xs'

signature
  sorts
    SBinder
    
  internal-constructors
    bindSVars: List(Strategy) * List(Typedid) -> SBinder
  
  native-operators
    createAnonymousName : String -> String
  
  arrows
    SBinder -bind-> D

rules

  D sdefs |- bindSVars([], []) -bind-> sdefs

  // 1. CallT without arguments
  D sdefs |- bindSVars([s | ss], [VarDec(x, dc) | tids]) -bind-> { x |--> sdef, binds }
  where
    s => CallT(SVar(name), ass, ats),
    ass => [],
    ats => [],
    sdefs[name] => sdef,
    sdef => SDefT(name', fss, fts, fs),
    bindSVars(ss, tids) -bind-> binds
  
  // 2. CallT with arguments
  D sdefs |- bindSVars([s | ss], [VarDec(x, dc) | tids]) -bind-> { x |--> SDefT(annonx, [], [], s), binds }
  where
    s => CallT(SVar(name), ass, ats),
    ass => [ass1 | ass2],
    ats => [ats1 | ats2],
    sdefs[name] => sdef,
    sdef => SDefT(name', fss, fts, fs),
    createAnonymousName(x) => annonx,
    bindSVars(ss, tids) -bind-> binds
  
  // 3. not a CallT
  D sdefs |- bindSVars([s | ss], [VarDec(x, dc) | tids]) -bind-> { x |--> SDefT(annonx, [], [], s), binds }
  where
    s =!=> CallT(SVar(name), ass, ats),
    createAnonymousName(x) => annonx,
    bindSVars(ss, tids) -bind-> binds
  
signature

  native-datatypes
	  "ds.manual.interpreter.AutoInterpInteropContext" as IC {
	    
	  }

  native-operators
    primCall : IC * String * List(Strategy) * List(T) * T -> Value

rules // primitives

  T t, IC ic |- PrimT(name, ass, ats) --> v
  where
    bl(ats) -blds-> ats_aterms,
    primCall(ic, name, ass, ats_aterms, t) => v // implement in Java
  
rules // dynamic call
  
  CallDynamic(target, ass, ats) --> v
  where
    Build(target) --> S(sname_aterm),
    isATermString(sname_aterm) == true,
    asATermString(sname_aterm) => sname_aterm_str,
    stringValue[sname_aterm_str]() => sname,
    CallT(SVar(sname), ass, ats) --> v
