module tracing

imports
  shared
  stratego-signatures

signature
  native-datatypes
    "org.spoofax.interpreter.core.StackTracer" as Trace {
      push : String -> Bool
    }
  
  internal-sorts
    TraceOp
  
  internal-constructors
    tracing : String * Strategy -> TraceOp
    pushTrace : String -> TraceOp
    popTrace : Value -> TraceOp
  
  arrows
    TraceOp --> Value
    TraceOp -trace-> Bool
  
  native-operators
    popOnFailure : Trace -> Bool
    popOnSuccess : Trace -> Bool

rules
  
  tracing(name, s) --> v
  where
    pushTrace(name) -trace-> _,
    s --> v,
    popTrace(v) -trace-> _
  
  (pushTrace(_), Trace t) -trace-> (true, Trace t)
  
  (popTrace(v), Trace t) -trace-> (true, Trace t)
  where
    v => F(),
    popOnFailure(t) => _
  
  (popTrace(v), Trace t) -trace-> (true, Trace t)
  where
    v => S(_),
    popOnSuccess(t) => _

