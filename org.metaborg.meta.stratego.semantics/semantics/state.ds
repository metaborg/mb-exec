module state

imports
  shared
  stratego-signatures
  
/* === Rules for the Variables === */
signature

  semantic-components
    VEnv  -> Map<String, VBox>

  native-datatypes
    "ds.manual.interpreter.VState" as VHeap {
      lookup : VEnv * String -> Value
      alloc  : VEnv * String -> VEnv
      allocs : VEnv * List(String) -> VEnv
      update : VEnv * String * Value -> VEnv
      allocupdate : VEnv * String * Value -> VEnv
    }
    
    "ds.manual.interpreter.VBox" as VBox { }

  internal-sorts
    VLookupResult
  
  internal-constructors
    VLookupResult : VEnv * Value -> VLookupResult

  internal-sorts
    VHeapOp

  internal-constructors
    VLookup : VEnv * String -> VHeapOp
    VUpdate : VEnv * String * Value -> VHeapOp
    VPush : VEnv * String -> VHeapOp
    VPushBatch : VEnv * List(String) -> VHeapOp
    VPushUpdate : VEnv * String * Value -> VHeapOp

  arrows
    VHeapOp -vlook-> VLookupResult
    VHeapOp -vupdate-> VEnv 
    VHeapOp -vinit-> VEnv
    
rules 
  
  (VLookup(venv, x), VHeap s) -vlook-> (VLookupResult(venv, v), VHeap s)
  where
    lookup[s](venv, x) => v
  
  (VUpdate(venv, x, v), VHeap s) -vupdate-> (venv, VHeap s)
  where
    update[s](venv, x, v) => _

  (VPush(venv, x), VHeap s) -vinit-> (venv', VHeap s)
  where
    alloc[s](venv, x) => venv'
  
  (VPushBatch(venv, xs), VHeap s) -vinit-> (venv', VHeap s)
  where
    allocs[s](venv, xs) => venv'
  
  (VPushUpdate(venv, x, v), VHeap s) -vinit-> (venv', VHeap s)
  where
    allocupdate[s](venv, x, v) => venv'

/* === Rules for the Strategies === */

signature
  semantic-components
    SEnv  -> Map<String, SBox>

  native-datatypes
    "ds.manual.interpreter.SState" as SHeap {
      lookup : SEnv * String -> Thunk
      alloc  : SEnv * String -> SEnv
      softalloc: SEnv * String -> SEnv
      allocupdate : SEnv * String * Thunk -> SEnv
      update : SEnv * String * Thunk -> SEnv
    }
    
    "ds.manual.interpreter.SBox" as SBox { }

  internal-sorts
    SLookupResult

  internal-constructors
    SLookupResult : SEnv * Thunk -> SLookupResult

  internal-sorts
    Thunk
  
  internal-constructors
    Thunk  : String * List(String) * List(String) * Strategy * VEnv * SEnv -> Thunk
    ThunkPlaceholder: -> Thunk

  internal-sorts
    SHeapOp
  
  internal-constructors
    SAlloc: SEnv * String -> SHeapOp
    SSoftAlloc: SEnv * String -> SHeapOp
    SParAlloc : SEnv * List(String) -> SHeapOp
    SUpdate : SEnv * String * Thunk -> SHeapOp
    SPush: SEnv * String * Thunk -> SHeapOp
    SLookup: SEnv * String -> SHeapOp
    
  arrows
    SHeapOp -slook-> SLookupResult
    SHeapOp -salloc-> SEnv
  
rules

  // Lookup a thunk by name at the starting SHeap address
  (SLookup(senv, name), SHeap s) -slook-> (SLookupResult(senv, thunk), SHeap s)
  where
    lookup[s](senv, name) => thunk
  
  // Push a new Thunk advancing the address of the top of the stack
  (SPush(senv, name, thunk), SHeap s) -salloc-> (senv', SHeap s)
  where
    allocupdate[s](senv, name, thunk) => senv'
  
  // Allocate a new location at the top of the SHeap and bind it to a placeholder
  (SAlloc(senv, name), SHeap s) -salloc-> (senv', SHeap s)
  where
    alloc[s](senv, name) => senv'
  
  // Allocate a new location at the top of the SHeap and bind it to a placeholder if no allocation for this name already exists
  (SSoftAlloc(senv, name), SHeap s) -salloc-> (senv', SHeap s)
  where
    softalloc[s](senv, name) => senv'
  
  (SUpdate(senv, name, thunk), SHeap s) -salloc-> (senv', SHeap s)
  where
    update[s](senv, name, thunk) => senv'

  (SParAlloc(senv, []), SHeap s) -salloc-> (senv, SHeap s)
  
  (SParAlloc(senv, [name | names]), SHeap s) -salloc-> (senv'', SHeap s'')
  where
    (SAlloc(senv, name), SHeap s) -salloc-> (senv', SHeap s'),
    (SParAlloc(senv', names), SHeap s') -salloc-> (senv'', SHeap s'')
  
    
