module state

imports
  base
  shared
  stratego-signatures
  
/* === Rules for the Variables === */
signature

  semantic-components
    VHeap -> Map<Int, Value>
    VEnv  -> Map<String, Int>

  internal-sorts
    VLookupResult
  
  internal-constructors
    VLookupResult : VEnv * Value -> VLookupResult

  internal-sorts
    VHeapOp

  internal-constructors
    VLookup : VEnv * String -> VHeapOp
    VUpdate : VEnv * String * Value -> VHeapOp
    VPush : VEnv * String -> VHeapOp
    VPushUpdate : VEnv * String * Value -> VHeapOp

  arrows
    VHeapOp -vlook-> VLookupResult
    VHeapOp -vupdate-> VEnv 
    VHeapOp -vbranch-> VEnv
    VHeapOp -vinit-> VEnv
    
rules 
  
  (VLookup(venv, x), VHeap s) -vlook-> (VLookupResult(venv, v), VHeap s)
  where
    venv[x] => addr,
    s[addr] => v
  
  (VUpdate(venv, x, v), VHeap s) -vupdate-> (venv, VHeap { addr |--> v, s})
  where
    venv[x] => addr

  (VPush(venv, x), VHeap s) -vinit-> ({x |--> addr, venv}, VHeap { addr |--> F(), s })
  where
    fresh => addr
  
  (VPushUpdate(venv, x, v), VHeap s) -vinit-> ({ x |--> addr, venv}, VHeap { addr |--> v, s})
  where
    fresh => addr


/* === Rules for the Strategies === */

signature
  semantic-components
    SHeap -> Map<Int, SBinding>
  
  internal-sorts
    SBinding
    SLookupResult
    SEnv

  internal-constructors
    SEnv : Int -> SEnv
    SMt : -> SBinding
    SBind : String * Thunk * Int -> SBinding
    SLookupResult : SEnv * SBinding -> SLookupResult

  internal-sorts
    Thunk
  
  internal-constructors
    Thunk  : String * List(String) * List(String) * Strategy * VEnv * SEnv -> Thunk
    ThunkPlaceholder: -> Thunk

  internal-sorts
    SHeapOp
  
  internal-constructors
    SInit : -> SHeapOp
    SAlloc: SEnv * String -> SHeapOp
    SMAlloc : SEnv * List(String) -> SHeapOp
    SPushBack: SEnv * String * Thunk -> SHeapOp
    SUpdate : SEnv * String * Thunk -> SHeapOp
    SPush: SEnv * String * Thunk -> SHeapOp
    SLookup: SEnv * String -> SHeapOp
    
  arrows
    SHeapOp -slook-> SLookupResult
    SHeapOp -salloc-> SEnv
  
  native-operators
    asSHeap: Map<Int, SBinding> -> SHeap
  
rules
  
  // init SHeap
  (SInit(), SHeap s) -salloc-> (SEnv(addr), SHeap s')
  where
    fresh => addr,
    asSHeap({ addr |--> SMt() }) => s'
  
  // Push a new Thunk advancing the address of the top of the stack
  (SPush(SEnv(addr), name, thunk), SHeap s) -salloc-> (SEnv(addr'), SHeap s')
  where
    fresh => addr',
    { addr' |--> SBind(name, thunk, addr), s} => s'
  
  // Push a new Thunk keeping the address of the top of the stack constant
  (SPushBack(SEnv(addr), name, thunk), SHeap s) -salloc-> (SEnv(addr), SHeap s'')
  where
    s[addr] => prev_top_thunk,
    fresh => addr',
    { addr' |--> prev_top_thunk, s } => s',
    { addr  |--> SBind(name, thunk, addr'), s'} => s''
  
  // Allocate a new location at the top of the SHeap and bind it to a placeholder
  (SAlloc(SEnv(addr), name), SHeap s) -salloc-> (SEnv(addr'), SHeap s')
  where
    fresh => addr',
    {addr' |--> SBind(name, ThunkPlaceholder(), addr), s} => s'
  
  (SMAlloc(SEnv(addr), []), SHeap s) -salloc-> (SEnv(addr), SHeap s)
  
  (SMAlloc(SEnv(addr), [name | names]), SHeap s) -salloc-> (SEnv(addr''), SHeap s'')
  where
    (SAlloc(SEnv(addr), name), SHeap s) -salloc-> (SEnv(addr'), SHeap s'),
    (SMAlloc(SEnv(addr'), names), SHeap s') -salloc-> (SEnv(addr''), SHeap s'')
  
  (SUpdate(SEnv(addr), name, thunk), SHeap s) -salloc-> (SEnv(addr), SHeap s'')
  where
    (SLookup(SEnv(addr), name), SHeap s) -slook-> (SLookupResult(SEnv(addr'), SBind(name', old_thunk, addr'')), SHeap s'),
    { addr' |--> SBind(name', thunk, addr''), s'} => s''
  
  // Lookup a thunk by name at the starting SHeap address
  (SLookup(SEnv(addr), name), SHeap s) -slook-> (SLookupResult(SEnv(addr), SBind(name', thunk, addr')), SHeap s)
  where
    s[addr] => SBind(name', thunk, addr'),
    name == name'
  
  (SLookup(SEnv(addr), name), SHeap s) -slook-> (v, SHeap s)
  where
    s[addr] => SBind(name', thunk, addr'),
    name != name',
    (SLookup(SEnv(addr'), name), SHeap s) -slook-> (v, SHeap s')
  
