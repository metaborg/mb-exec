module posix-file-test
imports sunit posix-file

strategies

  main = 
    test-suite(!"posix-file-test",
      fileno-tests
    ; access-tests
    ; aterm-io-tests
    ; char-output-tests
    ; cwd-tests
    )

  /**
   * @todo  Enable tmp-dir check, but compare canonical pathnames.
   */
  cwd-tests =
      where(getcwd => cwd)
    ; apply-and-check(!"chdir to tmpdir", chdir; getcwd, temp-dir, debug)
    ; apply-test(!"chdir to old cwd", chdir; getcwd, !cwd, !cwd)

    //; apply-test(!"chdir to home dir", chdir; getcwd, home-dir, home-dir)

  fileno-tests =
      apply-test(!"fileno on stdin  -- 1", fileno, stdin-stream, !0)
    ; apply-test(!"fileno on stdin  -- 2", fileno, stdin-stream, STDIN_FILENO)
    ; apply-test(!"fileno on stdout -- 1", fileno, stdout-stream, !1)
    ; apply-test(!"fileno on stdout -- 2", fileno, stdout-stream, STDOUT_FILENO)
    ; apply-test(!"fileno on stderr -- 1", fileno, stderr-stream, !2)
    ; apply-test(!"fileno on stderr -- 2", fileno, stderr-stream, STDERR_FILENO)

    ; where(new-temp-file => (tmp-name, tmp-fd))

    ; apply-test(!"fileno on temp file"
      ,   <fdopen> (tmp-fd, "w")
        ; ?stream
        ; <fileno> stream
        ; where(<fclose> stream)
      ,   !tmp-fd
      )

    ; where(<remove-file> tmp-name)

  access-tests = 
      apply-test(!"access -- 1", <access> (<id>, [Existence]), !"/", "/")
    ; apply-test(!"access -- 2", try(<access> (<id>, [Read])), !"/")

    // this used to result in a segmentation fault
    ; apply-test(!"access -- 3", try(<access> (<id>, [Read | []{1}])), !"/")
    ; apply-test(!"access -- 4", try(<access> (<id>, [Read]{1})), !"/")

  /*
      apply-and-fail(!"access --  1", <access> (<id>, [Write]), !"/proc/uptime")
    ; apply-test(    !"access --  2", <access> (<id>, [Existence]), !"/proc/uptime", !"/proc/uptime")
    ; apply-test(    !"access --  3", <access> (<id>, [Read]),  !"/proc/uptime", !"/proc/uptime")
    ; apply-and-fail(!"access --  4", <access> (<id>, [Read, Write]),  !"/proc/uptime", !"/proc/uptime")
    ; apply-and-fail(!"access --  5", <access> (<id>, []), !"/proc/uptime")
    ; apply-and-fail(!"access --  6", <access> (<id>, 1), !"/proc/uptime")
    ; apply-and-fail(!"access --  7", <access> (<id>, [Read]), !"/root")
    ; apply-test(    !"access --  8", <access> (<id>, [Existence]), !"/root", !"/root")
    ; apply-and-fail(!"access --  9", <access> (<id>, [Existence]), !"blablabla.blabla")
    ; apply-test(    !"access -- 10", <access> (<id>, [Execute, Read, Write]), !"posix-file-test", !"posix-file-test")
    ; apply-test(    !"access -- 11", <access> (<id>, [Existence]), !"posix-file-test", !"posix-file-test")
  */

  aterm-io-tests =
      aterm-io-tests(write-in-baf-to-stream)
    ; aterm-io-tests(write-in-taf-to-stream)
    ; aterm-io-tests(write-in-text-to-stream)

  aterm-io-tests(writer) =
      apply-test(!"ATerm IO -- 1", write-read-term(writer), !"Bla Bla Bla", !"Bla Bla Bla")
    // ; apply-test(!"ATerm IO -- 2", write-read-term(writer), !(), !()) TODO: fails ON TAF!
    ; apply-test(!"ATerm IO -- 3", write-read-term(writer), !1, !1)
    ; apply-test(!"ATerm IO -- 4", write-read-term(writer), !None(), !None())

      write-read-term(writer) =
        ?t
       ; <fopen> ("test.aterm", "w")
       ; <writer> (<id>, t)
       ; fclose
       ; <fopen> ("test.aterm", "r")
       ; where(read-from-stream => t)
       ; fclose
       ; <remove-file> "test.aterm"
       ; !t

  file-stream-tests = id

    ; apply-and-fail(!"fputc applied to a term that is not a stream"
      , <fputc> ('a', <id>)
      , !()
      )

    ; apply-and-fail(!"fputc applied to a term that is not a stream"
      , <fputc> ('a', <id>)
      , !Stream("foo")
      )

    ; apply-and-fail(!"fputs applied to a term that is not a stream"
      , <fputs> ("a", <id>)
      , !()
      )

    ; apply-and-fail(!"fputs applied to a term that is not a stream"
      , <fputc> ("a", <id>)
      , !Stream("foo")
      )

  char-output-tests = id

    ; apply-test(!"stdout char output test -- 1",
        <write-to-stream> (<id>, "This is stdout (1)")
      ; <fputs> ("This is stdout (2)\n", <id>)
      ; <puts> "This is stdout (3)"
      , stdout-stream
      , stdout-stream
      )

    ; apply-test(!"write a text file with fputc",
        <write-text-file-c> ("text-file.tmp", <id>)
      , !"This is a test file\nIt can be removed."
      , !()
      )

    ; apply-test(!"read a text file with fgetc",
        read-text-file
      , !"text-file.tmp"
      , !"This is a test file\nIt can be removed."
      )

    ; apply-test(!"write a text file with fputs",
        <write-text-file> ("text-file.tmp", <id>)
      , !"This is a test file\nIt can be removed."
      , !()
      )

    ; apply-test(!"read a text file with fgetc",
        read-text-file-2
      , !"text-file.tmp"
      , !"This is a test file\nIt can be removed."
      )

    ; where(<remove-file> "text-file.tmp")

  read-text-file =
    ?file
    ; <fopen> (file,"r") => stream
    ; read-text-from-stream
    ; where(<fclose> stream)

  read-text-file-2 =
    ?file
    ; <fopen> (file,"r") => stream
    ; rec x(![<fgetc> stream | <x> ()] <+ ![])
    ; where(<fclose> stream)
    ; implode-string

  read-text-file =
      ?file
    ; <fopen> (file,"r") => stream
    ; read-text-from-stream
    ; where(<fclose> stream)


  write-text-file =
    ?(file, string)
    ; <fopen> (file, "w")
    ; <fputs> (string, <id>)
    ; fclose

  write-text-file-c =
    ?(file, string)
    ; <fopen> (file, "w") => stream
    ; <explode-string> string
    ; map(<fputc> (<id>, stream))
    ; where(<fclose> stream)
    ; !()

